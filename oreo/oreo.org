#+setupfile: ../settings/README.org
#+property: header-args:hy :tangle yes

* Richy Traceback

#+begin_src hy
(import richy.traceback)
(.install richy.traceback :show-locals True)
#+end_src

* Imports

#+begin_src hy
(import click)
(import os)
#+end_src

** From

#+begin_src hy
(import addict [Dict :as D])
(import autoslot [SlotsMeta])
(import collections [OrderedDict])
(import hy [mangle unmangle])
(import hyrule [coll? dec])
(import importlib.util [spec-from-file-location module-from-spec])
(import itertools [islice])
(import richy.progress [Progress])
(import time [sleep])
(import uuid [uuid4])
#+end_src

* Requires

#+begin_src hy
(require hyrule [-> assoc])
#+end_src

*** Coconut

[[https://coconut.readthedocs.io/en/latest/index.html][Coconut]]:

#+begin_src hy
(try (import coconut *)
     (except [ImportError] None))
#+end_src

*** Toolz

[[https://github.com/pytoolz/toolz][Toolz]] and [[https://github.com/pytoolz/cytoolz/][CyToolz]]:

#+begin_src hy
(try (import cytoolz [first])
     (except [ImportError]
             (import toolz [first])))
#+end_src

* Module Installed

Adapted from [[https://www.geeksforgeeks.org/how-to-import-a-python-module-given-the-full-path/#:~:text=Inside%20explicit%20method-,Using%20importlib%20Package,-The%20importlib%20package][here]]

#+begin_src hy
(defn module-installed [path]
      (setv spec (-> os.path
                     (.basename path)
                     (.split ".")
                     (get 0)
                     (spec-from-file-location path)))
      (if spec
          (do (setv module (module-from-spec spec))
              (.exec-module spec.loader module)
              (return module))
          (return False)))
#+end_src

* Single Use Import

#+begin_src hy
(defn sui [*module attr]
      (return (if (setx module (module-installed *module))
                  (getattr module attr)
                  module)))
#+end_src

* Not Dots?

#+begin_src hy
(defn nots? [string] (not (or (= string ".") (= string ".."))))
#+end_src

* First or Last N Values

#+begin_src hy
(defn first-last-n [[iterable None] [last False] [number 0]]
      (setv iterable (tuple iterable)
            first-last-n/len (len iterable))
      (yield-from (if (and number iterable)
                      (if last
                          (cut iterable (- first-last-n/len number) first-last-n/len)
                          (cut iterable 0 number))
                      iterable)))
#+end_src

* Flatten Iterable

#+begin_src hy
(defn flatten [iterable [times None]]
      (setv lst [])
      (for [i iterable]
           (if (and (coll? i)
                    (or (is times None)
                        times))
               (.extend lst (flatten i :times (if times (dec times) times)))
               (.append lst i)))
      (return (if (= times 0) iterable lst)))
#+end_src

* Recursive Unmangle

#+begin_src hy
(defn recursive-unmangle [dct]
      (return (D (dfor [key value]
                       (.items dct)
                       [(unmangle key)
                        (if (isinstance value dict)
                            (recursive-unmangle value)
                            value)]))))
#+end_src

* Remove N Times
** Prefix

#+begin_src hy
(defn remove-prefix-n [string prefix [n 1]]
      (setv old-string "")
      (if n
          (for [i (range n)]
               (setv string (.removeprefix string prefix)))
          (if (= (len prefix) 1)
              (setv string (.lstrip string prefix))
              (while (!= old-string string)
                     (setv old-string string
                           string (.removeprefix string prefix)))))
      (return string))
#+end_src

** Suffix

#+begin_src hy
(defn remove-suffix-n [string suffix [n 1]]
      (setv old-string "")
      (if n
          (for [i (range n)]
               (setv string (.removesuffix string suffix)))
          (if (= (len suffix) 1)
              (setv string (.rstrip string suffix))
              (while (!= old-string string)
                     (setv old-string string
                           string (.removesuffix string suffix)))))
      (return string))
#+end_src

* Get Mangled or Unmangled Key

#+begin_src hy
(defn get-un-mangled [dct key [default None]]
      (return (or (.get dct (mangle key) None)
                  (.get dct (.replace (unmangle key) "_" "-") default))))
#+end_src

* ModuleCaller

Adapted from [[https://stackoverflow.com/users/11769765/friedrich][Friedrich's]] answer [[https://stackoverflow.com/a/61618555/10827766][here]].

#+begin_src hy
(defclass ModuleCaller)
#+end_src

* Integer Only

#+begin_src hy
(defn int? [value] (return (and (isinstance value int) (not (isinstance value bool)))))
#+end_src

* Either Instance

Adapted from [[https://stackoverflow.com/users/100297/martijn-pieters][Martijn Pieters']] answer [[https://stackoverflow.com/a/27475071/10827766][here]];
for more information, check out [[https://stackoverflow.com/users/179805/fletom][fletom's]] answer [[https://stackoverflow.com/a/17246726/10827766][here]],
and [[https://stackoverflow.com/users/210945/naught101][naught101's]] comment [[https://stackoverflow.com/questions/2611892/how-to-get-the-parents-of-a-python-class#comment70569175_2611939][here]]:

#+begin_src hy
(defn either? [first-type second-type #* args]
      (setv args (, first-type second-type #* args))
      (defn inner [cls]
            (return (if (hasattr cls "__mro__")
                        (gfor m cls.__mro__ :if (!= m object) m)
                        (, cls))))
      (any (gfor [i a]
                 (enumerate args)
                 :setv typle (tuple (flatten (gfor [j b]
                                                   (enumerate args)
                                                   :if (!= i j)
                                                   (inner (cond [(isinstance b ModuleCaller) (b)]
                                                                [(isinstance b type) b]
                                                                [True (type b)])))))
                 (cond [(isinstance a ModuleCaller) (issubclass (a) typle)]
                       [(isinstance a type) (issubclass a typle)]
                       [True (or (issubclass (type a) typle)
                                 (isinstance a typle))]))))
#+end_src

* The Meclair Class

Adapted from [[https://stackoverflow.com/users/36433/a-coady][A. Coady's]] answer [[https://stackoverflow.com/a/1800999/10827766][here]], as well as [[https://stackoverflow.com/users/302343/timur][Timur's]] answer [[https://stackoverflow.com/a/31537249/10827766][here]].

Remember that ~metaclasses~ use ~cls~ instead of ~self~!

#+begin_src hy
(defclass meclair [SlotsMeta]
#+end_src

** __init__

#+begin_src hy
(defn __init__ [cls #* args #** kwargs] (setv cls.Progress (Progress :auto-refresh False))))
#+end_src

* The Eclair Class

#+begin_src hy
(defclass eclair [:metaclass meclair]
#+end_src

** __init__

#+begin_src hy
(defn __init__ [self iterable name color [sleep 0.025]]
    (setv self.color color
          self.iterable (tuple iterable)
          self.len (len iterable)
          self.increment (/ 100 self.len)
          self.n 0
          self.name name
          self.sleep sleep)
#+end_src

*** Append preliminary invisible task to list of progress tasks

#+begin_src hy
(if (= (len self.__class__.Progress.task-ids) 0)
    (setv self.first-task (.add-task self.__class__.Progress f"[green]start" :total 0 :visible False)))
#+end_src

*** Append richy.progress task to list of progress tasks

Adapted from [[https://stackoverflow.com/users/100297/martijn-pieters][Martijn Pieters']] answer [[https://stackoverflow.com/a/26626707/10827766][here]],
as well as [[https://stackoverflow.com/users/9567/torsten-marek][Torsten Marek's]] answer [[https://stackoverflow.com/a/328882/10827766][here]]:

#+begin_src hy
(setv self.task (.add-task self.__class__.Progress f"[{self.color}]{self.name}" :total self.len :start False))
#+end_src

*** End of __init__

#+begin_src hy
)
#+end_src

** __iter__

#+begin_src hy
(defn __iter__ [self]
      (setv self.n 0)
      (if (= (len self.__class__.Progress.task-ids) 2)
          (do (.start self.__class__.Progress)
              (.start-task self.__class__.Progress (get self.__class__.Progress.task-ids 1)))
          (.start-task self.__class__.Progress self.task))
      (return self))
#+end_src

** __next__

#+begin_src hy
(defn __next__ [self]
      (if (< self.n self.len)
          (try (sleep self.sleep)
               (.update self.__class__.Progress self.task :advance self.increment :refresh True)
               (return (get self.iterable self.n))
               (finally (+= self.n 1)))
          (try (raise StopIteration)
               (finally (.stop-task self.__class__.Progress self.task)
                        (if self.__class__.Progress.finished
                            (.stop self.__class__.Progress))))))
#+end_src

** End of Eclair

#+begin_src hy
)
#+end_src

* Click
** Options

All options are adapted from [[https://stackoverflow.com/users/7311767/stephen-rauch][Stephen Rauch's]] answer [[https://stackoverflow.com/a/55881912/10827766][here]].

#+begin_src hy
(defclass Option [click.Option]
#+end_src

*** Static Methods
**** Name

#+begin_src hy
#@(staticmethod (defn static/name [name]
                      (-> name
                          (remove-prefix-n "-" :n 2)
                          (.replace "-" "_")
                          (.lower))))
#+end_src

**** Joined Options

#+begin_src hy
#@(staticmethod (defn static/opt-joined [opt-val opt-len]
                      (if (= opt-len 1)
                          (get opt-val 0)
                          (.join ", " (gfor opt opt-val :if (!= opt name) opt)))))
#+end_src

**** Option[s]

#+begin_src hy
#@(staticmethod (defn option? [opt-len] (if (= opt-len 1) "option" "options")))
#+end_src

**** Are Is?

#+begin_src hy
#@(staticmethod (defn is? [opt-len] (if (= opt-len 1) "is" "are")))
#+end_src

**** Da use?

#+begin_src hy
#@(staticmethod (defn da-use? [opt-len] (if (= opt-len 1) "the use" "one or more")))
#+end_src

**** Generate Help String

#+begin_src hy
#@(classmethod (defn static/gen-help [cls help end] (+ help "\nNOTE: This option " end)))
#+end_src

*** __init__

#+begin_src hy
(defn __init__ [self #* args #** kwargs]
#+end_src

**** Name

Naming convention taken from [[https://click.palletsprojects.com/en/8.0.x/options/#name-your-options][here]]:

#+begin_src hy
(setv name (cond [(= (len args) 1) (.static/name self.__class__ (get args 0))]
                 [(= (len args) 2) (if (.startswith (setx pre-name (get args 0)) "--")
                                       (.static/name self.__class__ pre-name)
                                       (.static/name self.__class__ (get args 1)))]
                 [(= (len args) 3) (get args 3)]))
#+end_src

**** Help

#+begin_src hy
(setv help (.get kwargs "help" ""))
#+end_src

**** Options
***** Exclusive Or

~xor:~ list of options this can't be used with

#+begin_src hy
(if (setx self.xor (.pop kwargs "xor" (,)))
    (do (setv self.xor-len (len self.xor)
              self.xor-joined (.static/opt-joined self.__class__ self.xor self.xor-len)
              self.xor-help #[f[is mutually exclusive with {(.option? self.__class__ self.xor-len)} {self.xor-joined}.]f])
        (+= help (.static/gen-help help self.xor-help))))
#+end_src

***** One Required

~one-req:~ list of options of which one or more must be used

#+begin_src hy
(setv self.one-req (or (.pop kwargs "one_req" None)
                       (.pop kwargs "one-req" (,))))
(if self.one-req
    (do (setv self.one-req-len (len self.one-req)
              self.one-req-joined (.static/opt-joined self.__class__ self.one-req self.one-req-len)
              self.one-req-help #[f[must be used if {(.option? self.__class__ self.one-req-len)} {self.one-req-joined} {(.is? self.__class__ self.one-req-len)} not.]f])
        (+= help (.static/gen-help help self.one-req-help))))
#+end_src

***** Requires One Of

~req-one-of:~ list of options of which one or more must be used with this option

#+begin_src hy
(setv self.req-one-of (or (.pop kwargs "req_one_of" None)
                          (.pop kwargs "req-one-of" (,))))
(if self.req-one-of
    (do (setv self.req-one-of-len (len self.req-one-of)
              self.req-one-of-joined (.static/opt-joined self.__class__ self.req-one-of self.req-one-of-len)
              self.req-one-of-help #[f[requires {(.da-use? self.__class__ self.req-one-of-len)} of {(.option? self.__class__ self.req-one-of-len)} {self.req-one-of-joined} as well.]f])
        (+= help (.static/gen-help help self.req-one-of-help))))
#+end_src

***** Requires All Of

~req-all-of:~ list of options of which all must be used with this option

#+begin_src hy
(setv self.req-all-of (or (.pop kwargs "req_all_of" None)
                          (.pop kwargs "req-all-of" (,))))
(if self.req-all-of
    (do (setv self.req-all-of-len (len self.req-all-of)
              self.req-all-of-joined (.static/opt-joined self.__class__ self.req-all-of self.req-all-of-len)
              self.req-all-of-help #[f[requires {(.option? self.__class__ self.req-all-of-len)} {self.req-all-of-joined} as well.]f])
        (+= help (.static/gen-help help ))))
#+end_src

**** Set Help

#+begin_src hy
(.update kwargs { "help" help })
#+end_src

**** Initialize Super

#+begin_src hy
(.__init__ (super) #* args #** kwargs)
#+end_src

**** End of __init__

#+begin_src hy
)
#+end_src

*** Handle Parse Result

~self.name in opts~ is being used because if absent,
the ~if~ condition would match regardless of whether this option is being used or not;
for example, if option ~a~ is mutually exclusive to option ~b~, using ~xor~, and ~self.name in opts~ wasn't used,
~command -a -b~ would fail as planned, but so would ~command -a~ and ~command -b~,
given that the option ~xor~ is still being parsed by the program.

#+begin_src hy
(defn handle-parse-result [self ctx opts args]
#+end_src

**** Options
***** Exclusive Or

#+begin_src hy
(if (and (in self.name opts)
         self.xor
         (any (gfor opt self.xor (in opt opts))))
    (raise (click.UsageError f"Sorry; {self.name} {self.xor-help}")))
#+end_src

***** One Required

#+begin_src hy
(if (not (and (not self.one-req)
              (in self.name opts)
              (any (gfor opt self.one-req (in opt opts)))))
    (raise (click.UsageError (+ "Sorry! "
                                (if (= self.one-req-len 1) "One of " "")
                                self.one-req-joined
                                " is required."))))
#+end_src

***** Requires One Of

#+begin_src hy
(if (and (in self.name opts)
         self.req-one-of
         (not (any (gfor opt self.req-one-of (in opt opts)))))
    (raise (click.UsageError f"Sorry; {self.name} {self.req-one-of-help}")))
#+end_src

***** Requires All Of

#+begin_src hy
(if (and (in self.name opts)
         self.req-all-of
         (not (all (gfor opt self.req-all-of (in opt opts)))))
    (raise (click.UsageError f"Sorry; {self.name} {self.req-all-of-help}")))
#+end_src

**** Handle Parse Result for Super

#+begin_src hy
(return (.handle-parse-result (super) ctx opts args))
#+end_src

**** End of Handle Parse Result

#+begin_src hy
)
#+end_src

*** End of Options

#+begin_src hy
)
#+end_src

* Gensing

A play on words between ~ginseng~ tea and ~gen-string~!

# To override ~slice~ functionality, refer to [[https://stackoverflow.com/users/100297/martijn-pieters][Martijn Pieters']] answer [[https://stackoverflow.com/a/16033058/10827766][here]].

** Tea

#+begin_src hy
(defclass tea [OrderedDict]
#+end_src

*** __init__

#+begin_src hy
(defn __init__ [self #* args #** kwargs]
#+end_src

*** Create and Update Super Dict

#+begin_src hy
(setv super-dict (dict (enumerate args)))
(.update super-dict kwargs)
#+end_src

**** Initialize Super with Super Dict

#+begin_src hy
(.__init__ (super) (gfor [k v] (.items super-dict) (, k v)))
#+end_src

**** End of __init__

#+begin_src hy
)
#+end_src

*** Gin

#+begin_src hy
(defn gin [self [delimiter " "] [override-type None]]
      (setv values (tuple (.values self)))
      (if override-type
          (setv values (tuple (map override-type values))))
      (try (setv first-value (get values 0))
           (except [IndexError] None)
           (else (return (cond [(isinstance first-value str) (.strip (.join delimiter (map str values)))]
                               [(isinstance first-value int) (sum (map int values))]
                               [(all (gfor value values (isinstance value (type first-value))))
                                (do (setv total first-value)
                                    (for [value (cut values 1 (len values))]
                                         (+= total value))
                                    total)]
                               [True (raise (TypeError "Sorry! All values in the tea must be of the same type to join!"))])))))
#+end_src

*** __call__

#+begin_src hy
(defn __call__ [self #* args #** kwargs] (.gin self #* args #** kwargs))
#+end_src

*** __str__

#+begin_src hy
(defn __str__ [self] (.gin self :override-type str))
#+end_src

*** Get Next Free Index

#+begin_src hy
(defn get-next-free-index [self]
      (setv current-len (len self)
            keys (.keys self))
      (if (in current-len keys)
          (while (in current-len keys)
                 (+= current-len 1)))
      (return current-len))
#+end_src

*** Append

#+begin_src hy
(defn append [self summand [key None]] (assoc self (or key (.get-next-free-index self)) summand))
#+end_src

*** Shifted

#+begin_src hy
(defn shifted [self #* args]
      (setv shift (.get-next-free-index self))
      (return (dfor [i s] (enumerate args) [(+ i shift) s])))
#+end_src

*** Extend

#+begin_src hy
(defn extend [self #* args #** kwargs]
      (.update self (.shifted self #* args))
      (.update self kwargs))
#+end_src

*** Glue

If the ~summand~ is a collection, pop its first value and merge it with the last value in the current tea, otherwise simply do the latter.

#+begin_src hy
(defn glue [self summand [override-type None]]
      (setv [last-key last-value] (.popitem self :last True)
            last-value (if override-type
                           (override-type last-value)
                           last-value)
            summand-is-collection (coll? summand)
            summand-is-dict (isinstance summand dict)
            summand (if (and summand-is-collection
                             (not summand-is-dict))
                        (list summand)
                        summand)
#+end_src

Adapted from [[https://stackoverflow.com/users/3218806/maxbellec][maxbellec's]] answer [[https://stackoverflow.com/a/39292086/10827766][here]]:

#+begin_src hy
            summand-first-value (if summand-is-collection
                                    (.pop summand
                                          (if summand-is-dict
                                              (next (iter summand))
                                              0))
                                    summand)
#+end_src

#+begin_src hy
            summand-first-value (if override-type
                                    (override-type summand-first-value)
                                    summand-first-value)
            summand-first-value (if (either? last-value summand-first-value)
                                    summand-first-value
                                    (raise (TypeError "Sorry! The last value of this tea and first value of the provided collection must be of the same type!"))))
      (assoc self last-key (+ last-value summand-first-value))
      (if summand-is-collection
          (.update self (if summand-is-dict
                            summand
                            (.shifted self #* summand)))))
#+end_src

*** __add__

#+begin_src hy
(defn __add__ [self summand]
      (setv scopy (deepcopy self))
      (cond [(isinstance summand dict) (.update scopy summand)]
            [(coll? summand) (.update scopy (.shifted scopy #* summand))]
            [True (assoc scopy (.get-next-free-index scopy) summand)])
      (return scopy))
#+end_src

*** __sub__

#+begin_src hy
(defn __sub__ [self subtrahend]
      (setv scopy (deeepcopy self))
      (for [key subtrahend]
           (del (get scopy key)))
      (return scopy))
#+end_src

*** End of Tea

#+begin_src hy
)
#+end_src